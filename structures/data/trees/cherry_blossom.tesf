id "cherry_blossom";

if (check(0,0,0) == "LAND") {
    fail;
}

str blockBelow = getBlock(0, -1, 0);
if (blockBelow != "minecraft:grass_block" &&
    blockBelow != "minecraft:podzol"      &&
    blockBelow != "minecraft:dirt"        &&
    blockBelow != "minecraft:coarse_dirt") {
    fail;
}

num radius = 4;
for(num px = -radius; px < radius; px = px + 1) {
  for(num pz = -radius; pz < radius; pz = pz + 1) {
    if(check(px,-1,pz) != "LAND") fail;
  }
}

num height = randomInt(4) + 6;


for(num current = 0; current < height; current = current + 1) {
    block(0, current, 0, "minecraft:oak_log");
    block(1, current, 0, "minecraft:oak_log");
    block(0, current, 1, "minecraft:oak_log");
    block(1, current, 1, "minecraft:oak_log");
    setMark(0, current, 0, "log");
    setMark(1, current, 0, "log");
    setMark(0, current, 1, "log");
    setMark(1, current, 1, "log");

    if(current > 2 && randomInt(3) == 0) { // offshoot
        num x = randomInt(4)-1;
        num z = randomInt(4)-1;
        if((x == 0 || x == 1) && (z == 0 || z == 1)) {
            if(randomInt(2) == 0) x = -1;
            else x = 2;
        }
        num r = randomInt(3)+3;
        for(num xi = -4; xi <= 4; xi = xi+1) {
            for(num zi = -4; zi <= 4; zi = zi+1) {
                if(pow(xi, 2) + pow(zi, 2) > r) continue; // circle
                if(getBlock(x+xi, current, z+zi) != "minecraft:air" || getMark(x+xi, current, z+zi) == "log") continue;
                block(x+xi, current, z+zi, "minecraft:acacia_leaves[distance=1]");
            }
        }
        block(x, current, z, "minecraft:oak_log");
        block(x, current+1, z, "minecraft:acacia_leaves[distance=1]");
        setMark(x, current, z, "log");
    }
}

// top layer leaves
for(num xi = -3.5; xi <= 3; xi = xi+1) {
    for(num zi = -3.5; zi <= 3; zi = zi+1) {
        if(pow(xi, 2) + pow(zi, 2) > 7) continue; // circle
        if(getBlock(xi, height+1, zi) != "minecraft:air" || getMark(xi, height+1, zi) == "log") continue;
        block(xi, height+1, zi, "minecraft:acacia_leaves[distance=1]");
    }
}

num r = 3.25 + randomInt(5)/4;
num r2 = r*r;

// bottom layer leaves
for(num xi = -4.5; xi <= 4; xi = xi+1) {
    for(num zi = -4.5; zi <= 4; zi = zi+1) {
        if(pow(xi, 2) + pow(zi, 2) > r2) continue; // circle
        if(getBlock(xi, height, zi) != "minecraft:air" || getMark(xi, height, zi) == "log") continue;
        block(xi, height, zi, "minecraft:acacia_leaves[distance=1]");
    }
}

block(0, height+2, 0, "minecraft:acacia_leaves[distance=1]"); // top leaf
block(1, height+2, 0, "minecraft:acacia_leaves[distance=1]"); // top leaf
block(0, height+2, 1, "minecraft:acacia_leaves[distance=1]"); // top leaf
block(1, height+2, 1, "minecraft:acacia_leaves[distance=1]"); // top leaf